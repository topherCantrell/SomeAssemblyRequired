                         ; "Hello World" program for Atari 2600
                         .PROCESSOR 6502
                         
                         ; Hardware registers
                         .EQU  VSYNC  = 0x0000
                         .EQU  VBLANK = 0x0001
                         .EQU  WSYNC  = 0x0002
                         .EQU  COLUPF = 0x0008
                         .EQU  COLUBK = 0x0009
                         .EQU  PF0    = 0x000D
                         .EQU  PF1    = 0x000E
                         .EQU  PF2    = 0x000F
                         .EQU  INTIM  = 0x0284
                         .EQU  TIM64T = 0x0296
                         
                         0xF800:
f800 : 78                  SEI            ; Turn off interrupts
f801 : d8                  CLD            ; Clear the "decimal" flag
f802 : a2 ff               LDX  #0xFF     ; Start stack ...
f804 : 9a                  TXS            ; ... at end of RAM        
f805 : a9 c0               LDA  #0xC0     ; Background color ...
f807 : 85 09               STA  COLUBK    ; ... greenish        
f809 : a9 40               LDA  #0x40     ; Playfield ...
f80b : 85 08               STA  COLUPF    ; ... redish    
                                 
                         ;============= BETWEEN FRAMES ===================
                         ;  (start here at the END of every frame)
                         ;
                         NewFrame: 
f80d : a9 02               LDA   #0x02     ; D1 bit ON
f80f : 85 02               STA   WSYNC     ; Wait for the end of the current line        
f811 : 85 01               STA   VBLANK    ; Turn the electron beam off
f813 : 85 02               STA   WSYNC     ; Wait for all ...
f815 : 85 02               STA   WSYNC     ; ... the electrons ...
f817 : 85 02               STA   WSYNC     ; ... to drain out.
f819 : 85 00               STA   VSYNC     ; Trigger the vertical sync signal
f81b : 85 02               STA   WSYNC     ; Hold the vsync signal for ...
f81d : 85 02               STA   WSYNC     ; ... three ...
f81f : 85 02               STA   WSYNC     ; ... scanlines
f821 : a9 00               LDA   #0x00     ; D1 bit OFF                
f823 : 85 00               STA   VSYNC     ; Release the vertical sync signal
f825 : a9 2b               LDA   #43       ; Set timer to 43*64 = 2752 machine ...
f827 : 8d 96 02            STA   TIM64T    ; ... cycles 2752/(228/3) = 36 scanlines
                         
                         ; Process game-logic here.
                         
                         WaitTimer:
f82a : ad 84 02            LDA   INTIM     ; Wait for the electron gun ...
f82d : d0 fb               BNE   WaitTimer ; ... to move to upper left corner  
f82f : 85 02               STA   WSYNC     ; 37th scanline
f831 : a9 00               LDA   #0x00     ; Turn the ...
f833 : 85 01               STA   VBLANK    ; ... electron beam back on            
                             
                         ;============= VISIBLE PART OF FRAME ==============
                                 
f835 : a2 dc               LDX   #220      ; 220 scan lines (~213 visible on my TV)
f837 : a9 01               LDA   #1        ; Start value for playfield
                         DrawRow:
f839 : 85 0d               STA   PF0       ; Draw the playfield pattern      
f83b : 85 0e               STA   PF1
f83d : 85 0f               STA   PF2          
f83f : 85 02               STA   WSYNC     ; Wait for the end of the line
f841 : 69 01               ADC   #1        ; Bump the playfield pattern
f843 : ca                  DEX             ; Count ...
f844 : d0 f3               BNE   DrawRow   ; ... 220 lines
                         
f846 : 4c 0d f8            JMP   NewFrame  ; Next TV frame
                         
                         ;=== 6502 Hardware vectors at the end of memory ===
                         
                         0xFFFA:
fffa : 00 00               .WORD 0x0000    ; NMI vector (not used)
fffc : 00 f8               .WORD 0xF800    ; Reset vector (top of program)
fffe : 00 00               .WORD 0x0000    ; IRQ vector (not used)

***** Labels *****

F839 DrawRow
F80D NewFrame
F82A WaitTimer

***** Equates *****

0009 COLUBK
0008 COLUPF
0284 INTIM
000D PF0
000E PF1
000F PF2
0296 TIM64T
0001 VBLANK
0000 VSYNC
0002 WSYNC
