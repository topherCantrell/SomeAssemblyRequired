F800 78          SEI             ; Turn off interrupts
F801 D8          CLD             ; Clear the "decimal" flag
F802 A2 FF       LDX #0xFF       ; Start stack ...
F804 9A          TXS             ; ... at end of RAM (01FF)    
F805 A9 C0       LDA #0xC0       ; Background color ...
F807 8D 09 00    STA 0x0009      ; ... greenish        
F80A A9 40       LDA #0x40       ; Playfield ...
F80C 8D 08 00    STA 0x0008      ; ... redish    
F80F A9 02       LDA #0x02       ; D1 bit ON (vsync and vblank bits)
F811 8D 02 00    STA 0x0002      ; Wait for the end of the current line        
F814 8D 01 00    STA 0x0001      ; Start the vertical blanking
F817 8D 02 00    STA 0x0002      ; Hold ...
F81A 8D 02 00    STA 0x0002      ; ... for three ...
F81D 8D 02 00    STA 0x0002      ; ... scanlines.
F820 8D 00 00    STA 0x0000      ; Trigger the vertical sync signal
F823 8D 02 00    STA 0x0002      ; Hold the vsync signal for ...
F826 8D 02 00    STA 0x0002      ; ... three ...
F829 8D 02 00    STA 0x0002      ; ... scanlines
F82C A9 00       LDA #0x00       ; D1 bit OFF (vsync and vblank bits)
F82E 8D 00 00    STA 0x0000      ; Release the vertical sync signal
F831 A9 2B       LDA #0x2B       ; Set timer to 43*64 = 2752 machine ...
F833 8D 96 02    STA 0x0296      ; ... cycles 2752/(228/3) = 36 scanlines
F836 AD 84 02    LDA 0x0284      ; Wait for the visible ...
F839 D0 FB       BNE F836        ; ... part of the screen  
F83B 8D 02 00    STA 0x0002      ; 37th scanline
F83E A9 00       LDA #0x00       ; Release the ...
F840 8D 01 00    STA 0x0001      ; ... blanking            
F843 A2 DC       LDX #0xDC       ; 220 scan lines (~213 visible on my TV)
F845 A9 01       LDA #0x01       ; Start value for playfield
F847 8D 0D 00    STA 0x000D      ; Draw the playfield pattern      
F84A 8D 0E 00    STA 0x000E      ;
F84D 8D 0F 00    STA 0x000F      ;
F850 8D 02 00    STA 0x0002      ; Wait for the end of the line
F853 18          CLC             ; Clear the carry for next add
F854 69 01       ADC #0x01       ; Bump the playfield pattern
F856 CA          DEX             ; Count ...
F857 D0 EE       BNE F847        ; ... 220 lines
F859 4C 0F F8    JMP 0xF80F      ; Next TV frame
FFFA 00 F8                       ; NMI vector (not used)
FFFC 00 F8                       ; Reset vector (top of program)
FFFE 00 F8                       ; IRQ vector (not used)
